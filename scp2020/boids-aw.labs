# A variant of the Boids algorithm from
# C. W. Reynolds, “Flocks, herds and schools: A distributed behavioral model,” in 14th Annual Conference on Computer Graphics and Interactive Techniques (SIGGRAPH), Anaheim, CA, USA, 1987, vol. 21, pp. 25–34.
# This variant implements Alignment on a Wrap-around 2D grid
# (i.e. an agent can cross the edge of the arena and get to the opposite side).

system {
    extern = _birds, _grid, _delta
    spawn = Bird: _birds
}

stigmergy Alignment {
    link =
        ((x of c1 - x of c2) * (x of c1 - x of c2)) + 
        ((y of c1 - y of c2) * (y of c1 - y of c2)) <= _delta * _delta

    dirx, diry: [-1, 1], [-1, 1]

}

agent Bird {
    interface = x: 0.._grid; y: 0.._grid
    stigmergies = Alignment
    
    Behavior = Move; Behavior
    Move = x, y <- (x + dirx) % _grid, (y + diry) % _grid

}

check {
    Consensus = finally forall Bird b1, forall Bird b2, 
        dirx of b1 = dirx of b2
        and
        diry of b1 = diry of b2
}
